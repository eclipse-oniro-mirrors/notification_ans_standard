# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/notification/ans_standard/notification.gni")
import("//build/ohos.gni")

group("ans_targets") {
  deps = [ ":libans" ]
}

config("public_ans_config") {
  include_dirs = [ "${services_path}/ans/include" ]
  defines = [
    "APP_LOG_TAG = \"Ans\"",
    "LOG_DOMAIN = 0xD002200",
  ]
}

ohos_shared_library("libans") {
  include_dirs = [ "include" ]

  sources = [
    "src/access_token_helper.cpp",
    "src/advanced_notification_service.cpp",
    "src/advanced_notification_service_ability.cpp",
    "src/bundle_manager_helper.cpp",
    "src/notification_preferences.cpp",
    "src/notification_preferences_database.cpp",
    "src/notification_preferences_info.cpp",
    "src/notification_slot_filter.cpp",
    "src/notification_subscriber_manager.cpp",
    "src/permission_filter.cpp",
    "src/reminder_data_manager.cpp",
    "src/reminder_event_manager.cpp",
    "src/reminder_timer_info.cpp",
    "src/system_event_observer.cpp",
  ]

  configs = [
    ":public_ans_config",
    "//utils/native/base:utils_config",
  ]

  defines = []

  deps = [
    ":external.json",
    "${core_path}:ans_core",
    "${frameworks_path}/ans/dialog/dialog_ui/js:dialog_js_files_etc",
    "${frameworks_path}/ans/native:ans_innerkits",
    "//foundation/arkui/ace_engine/interfaces/inner_api/ui_service_manager:ui_service_mgr",
    "//utils/native/base:utils",
  ]

  if (is_double_framework) {
    cflags = [ "-DCONFIG_DUAL_FRAMEWORK" ]
  }

  if (distributed_notification_supported) {
    defines += [ "DISTRIBUTED_NOTIFICATION_SUPPORTED" ]
    deps += [ "${services_path}/distributed:libans_distributed" ]
    include_dirs += [ "${services_path}/distributed/include" ]
  }

  external_deps = [
    "ability_runtime:abilitykit_native",
    "ability_runtime:app_manager",
    "ability_runtime:wantagent_innerkits",
    "access_token:libaccesstoken_sdk",
    "distributeddatamgr:distributeddata_inner",
    "multimedia_image_standard:image_native",
    "multimedia_media_standard:media_client",
    "os_account:os_account_innerkits",
    "time_native:time_service",
    "window_manager:libwm",
  ]
  external_deps += ans_standard_external_deps

  subsystem_name = "notification"
  part_name = "ans_standard"
}

ohos_prebuilt_etc("external.json") {
  source =
      "//base/notification/ans_standard/services/ans/resource/external.json"
  relative_install_dir = "notification_template"
  subsystem_name = "notification"
  part_name = "ans_standard"
}
